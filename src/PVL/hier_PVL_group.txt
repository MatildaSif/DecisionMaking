model {
  
  # ===== GROUP-LEVEL PRIORS FOR CONTROLS =====
  mu_w_ctr ~ dnorm(0,1)T(0,) 
  mu_A_ctr ~ dnorm(0,1) 
  mu_theta_ctr ~ dnorm(0,1)T(0,) 
  mu_a_ctr ~ dnorm(0,1)T(0,1) 
  
  lambda_w_ctr ~ dgamma(2.5/2, 0.01/2)
  lambda_A_ctr ~ dgamma(2.5/2, 0.01/2)
  lambda_theta_ctr ~ dgamma(2.5/2, 0.01/2)
  lambda_a_ctr ~ dgamma(2.5/2, 0.01/2)
  
  # ===== GROUP-LEVEL PRIORS FOR HEROIN USERS =====
  mu_w_her ~ dnorm(0,1)T(0,) 
  mu_A_her ~ dnorm(0,1) 
  mu_theta_her ~ dnorm(0,1)T(0,) 
  mu_a_her ~ dnorm(0,1)T(0,1) 
  
  lambda_w_her ~ dgamma(2.5/2, 0.01/2)
  lambda_A_her ~ dgamma(2.5/2, 0.01/2)
  lambda_theta_her ~ dgamma(2.5/2, 0.01/2)
  lambda_a_her ~ dgamma(2.5/2, 0.01/2)
  
  # ===== GROUP-LEVEL PRIORS FOR AMPHETAMINE USERS =====
  mu_w_amp ~ dnorm(0,1)T(0,) 
  mu_A_amp ~ dnorm(0,1) 
  mu_theta_amp ~ dnorm(0,1)T(0,) 
  mu_a_amp ~ dnorm(0,1)T(0,1) 
  
  lambda_w_amp ~ dgamma(2.5/2, 0.01/2)
  lambda_A_amp ~ dgamma(2.5/2, 0.01/2)
  lambda_theta_amp ~ dgamma(2.5/2, 0.01/2)
  lambda_a_amp ~ dgamma(2.5/2, 0.01/2)
  
  # ===== COMPUTE GROUP DIFFERENCES =====
  # Controls vs Heroin
  diff_w_ctr_her <- mu_w_ctr - mu_w_her
  diff_A_ctr_her <- mu_A_ctr - mu_A_her
  diff_theta_ctr_her <- mu_theta_ctr - mu_theta_her
  diff_a_ctr_her <- mu_a_ctr - mu_a_her
  
  # Controls vs Amphetamine
  diff_w_ctr_amp <- mu_w_ctr - mu_w_amp
  diff_A_ctr_amp <- mu_A_ctr - mu_A_amp
  diff_theta_ctr_amp <- mu_theta_ctr - mu_theta_amp
  diff_a_ctr_amp <- mu_a_ctr - mu_a_amp
  
  # Heroin vs Amphetamine
  diff_w_her_amp <- mu_w_her - mu_w_amp
  diff_A_her_amp <- mu_A_her - mu_A_amp
  diff_theta_her_amp <- mu_theta_her - mu_theta_amp
  diff_a_her_amp <- mu_a_her - mu_a_amp
  
  # ===== CONTROLS =====
  for (s in 1:nsubs_ctr) {
    w_ctr[s] ~ dnorm(mu_w_ctr, lambda_w_ctr)T(0,)
    A_ctr[s] ~ dnorm(mu_A_ctr, lambda_A_ctr)
    theta_ctr[s] ~ dnorm(mu_theta_ctr, lambda_theta_ctr)T(0,)
    a_ctr[s] ~ dnorm(mu_a_ctr, lambda_a_ctr)T(0,1)
    
    Ev_ctr[s,1,1] ~ dnorm(0,0.01)
    Ev_ctr[s,1,2] ~ dnorm(0,0.01)
    Ev_ctr[s,1,3] ~ dnorm(0,0.01)
    Ev_ctr[s,1,4] ~ dnorm(0,0.01)
    
    for (t in 2:ntrials_ctr[s]) {
      
      for (d in 1:4) {
        u_ctr[s, t, d] <- ifelse(X_ctr[s, t-1] < 0, 
                                 -w_ctr[s] * abs(X_ctr[s, t-1])^A_ctr[s], 
                                 X_ctr[s,t-1]^A_ctr[s])
        Ev_update_ctr[s, t, d] <- Ev_ctr[s, t-1, d] + (a_ctr[s] * (u_ctr[s, t, d] - Ev_ctr[s, t-1, d]))
        Ev_ctr[s,t,d] <- ifelse(x_ctr[s,t-1] == d, Ev_update_ctr[s,t,d], Ev_ctr[s, t-1, d])
        exp_p_ctr[s, t, d] <- exp(theta_ctr[s]*Ev_ctr[s, t, d])
      }
      
      for (d in 1:4) {
        p_ctr[s, t, d] <- exp_p_ctr[s, t, d]/sum(exp_p_ctr[s, t, ])
      }
      
      x_ctr[s,t] ~ dcat(p_ctr[s,t, ])
    }
  }
  
  # ===== HEROIN USERS =====
  for (s in 1:nsubs_her) {
    w_her[s] ~ dnorm(mu_w_her, lambda_w_her)T(0,)
    A_her[s] ~ dnorm(mu_A_her, lambda_A_her)
    theta_her[s] ~ dnorm(mu_theta_her, lambda_theta_her)T(0,)
    a_her[s] ~ dnorm(mu_a_her, lambda_a_her)T(0,1)
    
    Ev_her[s,1,1] ~ dnorm(0,0.01)
    Ev_her[s,1,2] ~ dnorm(0,0.01)
    Ev_her[s,1,3] ~ dnorm(0,0.01)
    Ev_her[s,1,4] ~ dnorm(0,0.01)
    
    for (t in 2:ntrials_her[s]) {
      
      for (d in 1:4) {
        u_her[s, t, d] <- ifelse(X_her[s, t-1] < 0, 
                                 -w_her[s] * abs(X_her[s, t-1])^A_her[s], 
                                 X_her[s,t-1]^A_her[s])
        Ev_update_her[s, t, d] <- Ev_her[s, t-1, d] + (a_her[s] * (u_her[s, t, d] - Ev_her[s, t-1, d]))
        Ev_her[s,t,d] <- ifelse(x_her[s,t-1] == d, Ev_update_her[s,t,d], Ev_her[s, t-1, d])
        exp_p_her[s, t, d] <- exp(theta_her[s]*Ev_her[s, t, d])
      }
      
      for (d in 1:4) {
        p_her[s, t, d] <- exp_p_her[s, t, d]/sum(exp_p_her[s, t, ])
      }
      
      x_her[s,t] ~ dcat(p_her[s,t, ])
    }
  }
  
  # ===== AMPHETAMINE USERS =====
  for (s in 1:nsubs_amp) {
    w_amp[s] ~ dnorm(mu_w_amp, lambda_w_amp)T(0,)
    A_amp[s] ~ dnorm(mu_A_amp, lambda_A_amp)
    theta_amp[s] ~ dnorm(mu_theta_amp, lambda_theta_amp)T(0,)
    a_amp[s] ~ dnorm(mu_a_amp, lambda_a_amp)T(0,1)
    
    Ev_amp[s,1,1] ~ dnorm(0,0.01)
    Ev_amp[s,1,2] ~ dnorm(0,0.01)
    Ev_amp[s,1,3] ~ dnorm(0,0.01)
    Ev_amp[s,1,4] ~ dnorm(0,0.01)
    
    for (t in 2:ntrials_amp[s]) {
      
      for (d in 1:4) {
        u_amp[s, t, d] <- ifelse(X_amp[s, t-1] < 0, 
                                 -w_amp[s] * abs(X_amp[s, t-1])^A_amp[s], 
                                 X_amp[s,t-1]^A_amp[s])
        Ev_update_amp[s, t, d] <- Ev_amp[s, t-1, d] + (a_amp[s] * (u_amp[s, t, d] - Ev_amp[s, t-1, d]))
        Ev_amp[s,t,d] <- ifelse(x_amp[s,t-1] == d, Ev_update_amp[s,t,d], Ev_amp[s, t-1, d])
        exp_p_amp[s, t, d] <- exp(theta_amp[s]*Ev_amp[s, t, d])
      }
      
      for (d in 1:4) {
        p_amp[s, t, d] <- exp_p_amp[s, t, d]/sum(exp_p_amp[s, t, ])
      }
      
      x_amp[s,t] ~ dcat(p_amp[s,t, ])
    }
  }
}